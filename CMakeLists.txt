# PenMath Root CMake
cmake_minimum_required(VERSION 3.24 FATAL_ERROR)

get_filename_component(PROJECT_NAME ${CMAKE_CURRENT_SOURCE_DIR} NAME)

project(${PROJECT_NAME} VERSION 1.0.0 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED True)

set(TARGET_NAME PenMath)

# Headers
file(GLOB_RECURSE TARGET_HEADER_FILES 
    ${CMAKE_CURRENT_SOURCE_DIR}/Header/*.h
    ${CMAKE_CURRENT_SOURCE_DIR}/Header/*.hpp
    ${CMAKE_CURRENT_SOURCE_DIR}/Header/*.inl
)
list(FILTER TARGET_HEADER_FILES EXCLUDE REGEX ${CMAKE_CURRENT_BINARY_DIR})

# Sources
file(GLOB_RECURSE TARGET_SOURCE_FILES 
    ${CMAKE_CURRENT_SOURCE_DIR}/Source/*.c
    ${CMAKE_CURRENT_SOURCE_DIR}/Source/*.cc
    ${CMAKE_CURRENT_SOURCE_DIR}/Source/*.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/Source/*.cxx
    ${CMAKE_CURRENT_SOURCE_DIR}/Source/*.c++
)
list(FILTER TARGET_SOURCE_FILES EXCLUDE REGEX ${CMAKE_CURRENT_BINARY_DIR})

set(TARGET_FILES ${TARGET_HEADER_FILES} ${TARGET_SOURCE_FILES})
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${TARGET_FILES})

# Library
add_library(${TARGET_NAME} STATIC ${TARGET_FILES})

target_include_directories(${TARGET_NAME}
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/Header>
        $<INSTALL_INTERFACE:include>
)

# Installation rules
include(GNUInstallDirs)
install(TARGETS ${TARGET_NAME}
    EXPORT PenMathEx
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

install(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/Header/ DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

install(
    EXPORT PenMathEx
    FILE PenMathLib.cmake
    NAMESPACE PenMath::
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/PenMath
)

include(CMakePackageConfigHelpers)
write_basic_package_version_file(
    "${CMAKE_CURRENT_BINARY_DIR}/PenMathConfigVersion.cmake"
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
)

configure_file(
    PenMathConfig.cmake.in
    "${CMAKE_CURRENT_BINARY_DIR}/PenMathConfig.cmake"
    @ONLY
)

install(FILES
    "${CMAKE_CURRENT_BINARY_DIR}/PenMathConfig.cmake"
    "${CMAKE_CURRENT_BINARY_DIR}/PenMathConfigVersion.cmake"
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/PenMath
)

# Optional: Visual Studio startup project
if(MSVC)
    set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${TARGET_NAME})
endif()
